Перем ЮнитТест;

#Область ОбработчикиСобытийМодуля

Функция Версия() Экспорт
	Возврат "1.0";
КонецФункции

Функция ПолучитьСписокТестов(МенеджерТестирования) Экспорт
	
	ЮнитТест = МенеджерТестирования;

	СписокТестов = Новый Массив;
	СписокТестов.Добавить("ТестКонструктор");
	СписокТестов.Добавить("TestConstructor");
	СписокТестов.Добавить("ТестИмя");
	СписокТестов.Добавить("ТестИмяБазовогоТипа");
	СписокТестов.Добавить("ТестИмяТипаЭлемента");
	
	Возврат СписокТестов;

КонецФункции

#КонецОбласти

#Область ОбработчикиТестирования

Процедура ТестКонструктор() Экспорт

	ОпределениеТипа = Новый ОпределениеПростогоТипаXS;
	
	ЮнитТест.ПроверитьРавенство(ТипЗнч(ОпределениеТипа), Тип("ОпределениеПростогоТипаXS"));
	ЮнитТест.ПроверитьРавенство(ОпределениеТипа.ТипКомпоненты, ТипКомпонентыXS.ОпределениеПростогоТипа);
	
КонецПроцедуры

Procedure TestConstructor() Export

	TypeDefinition = New XSSimpleTypeDefinition;

	ЮнитТест.ПроверитьРавенство(TypeOf(TypeDefinition), Type("XSSimpleTypeDefinition"));
	ЮнитТест.ПроверитьРавенство(TypeDefinition.ComponentType, XSComponentType.SimpleTypeDefinition);

EndProcedure

Процедура ТестИмя() Экспорт

	ОпределениеТипа = Новый ОпределениеПростогоТипаXS;

	ОпределениеТипа.Имя = "test";

	ЮнитТест.ПроверитьРавенство(ОпределениеТипа.Имя, "test");

КонецПроцедуры

Процедура ТестИмяБазовогоТипа() Экспорт

	ОпределениеТипа = Новый ОпределениеПростогоТипаXS;
	ИмяТипа = Новый РасширенноеИмяXML("http://www.w3.org/2001/XMLSchema", "string");

	ОпределениеТипа.ИмяБазовогоТипа = ИмяТипа;

	ЮнитТест.ПроверитьРавенство(ОпределениеТипа.Вариант, ВариантПростогоТипаXS.Атомарная);
	ЮнитТест.ПроверитьРавенство(ОпределениеТипа.ИмяБазовогоТипа, ИмяТипа);

КонецПроцедуры

Процедура ТестИмяТипаЭлемента() Экспорт

	ОпределениеТипа = Новый ОпределениеПростогоТипаXS;
	ИмяТипа = Новый РасширенноеИмяXML("http://www.w3.org/2001/XMLSchema", "string");

	ОпределениеТипа.Вариант = ВариантПростогоТипаXS.Список;
	ОпределениеТипа.ИмяТипаЭлемента = ИмяТипа;

	ЮнитТест.ПроверитьРавенство(ОпределениеТипа.Вариант, ВариантПростогоТипаXS.Список);
	ЮнитТест.ПроверитьРавенство(ОпределениеТипа.ИмяТипаЭлемента, ИмяТипа);

КонецПроцедуры

#КонецОбласти