Перем юТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	юТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;
	
	ВсеТесты.Добавить("ТестДолжен_ПолучитьПутьКOscript");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьРазделениеСтрокПриВыводе");
	
	Возврат ВсеТесты;
	
КонецФункции

Процедура ТестДолжен_ПолучитьПутьКOscript() Экспорт
	
	Путь = Новый Файл(ПутьОСкрипт());
	
	юТест.ПроверитьИстину(Путь.Существует());
	
КонецПроцедуры

Функция СтрокаЗапускаОСкрипта(Знач ПутьКИсполняемомуМодулю)

	СИ = Новый СистемнаяИнформация;
	Если Найти(СИ.ВерсияОС, "Windows") > 0 Тогда
		Возврат ПутьКИсполняемомуМодулю;
	КонецЕсли;

	Возврат "mono """ + ПутьКИСполняемомуМодулю + """";

КонецФункции

Функция УникальнаяЧастьИмени()
	Возврат СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-","");
КонецФункции

Функция ПолучитьВыводДляСкрипта(Знач Путь, Знач ТекстСкрипта)
	
	Перем ИмяФайла, СтрокаЗапуска;

	ИмяФайлаОСкрипта = КаталогВременныхФайлов() + "/" + УникальнаяЧастьИмени();
	
	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайлаОСкрипта);
	ЗаписьТекста.Записать(ТекстСкрипта);
	ЗаписьТекста.Закрыть();

	// НАДО: запуск под Windows

	ИмяФайлаСистемногоСкриптаЗапуска = КаталогВременныхФайлов() + "/" + УникальнаяЧастьИмени();
	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайлаСистемногоСкриптаЗапуска);
	ЗаписьТекста.ЗаписатьСтроку("bash -s <<<CALLEOF");
	ЗаписьТекста.ЗаписатьСтроку("SCRIPT_FILENAME=" + ИмяФайлаОСкрипта + " " 
		+ СтрокаЗапускаОСкрипта(Путь) + " -cgi")
	;
	ЗаписьТекста.ЗаписатьСтроку("CALLEOF");
	ЗаписьТекста.Закрыть();

	СтрокаЗапуска = "bash " + ИмяФайлаСистемногоСкриптаЗапуска;

	Процесс = СоздатьПроцесс(СтрокаЗапуска,,Истина);
	Процесс.Запустить();
	Поток = Процесс.ПотокВывода;

	Стр = НормализоватьПереводыСтрок(Поток.Прочитать());

	Возврат Стр;

КонецФункции

Процедура ТестДолжен_ПроверитьРазделениеСтрокПриВыводе() Экспорт
	
	Путь = ПутьОСкрипт();

	ОжидаемыеЗаголовки = 
		"Content-type: text/html
		|Content-encoding: utf-8
		|
		|"
	;
	
	ПроверкаПереводаСтрок = ПолучитьВыводДляСкрипта(Путь,
		"Сообщить(""Строка1""); Сообщить(""Строка2"");"
	);
	
	ОжидаемыйВывод = ОжидаемыеЗаголовки +
		"Строка1
		|Строка2
		|"
	;
	юТест.ПроверитьРавенство(ОжидаемыйВывод, ПроверкаПереводаСтрок);
	
КонецПроцедуры

Функция ПутьОСкрипт()
	Возврат КаталогПрограммы() + "/oscript.exe";
КонецФункции

Функция НормализоватьПереводыСтрок(Знач ИсходнаяСтрока)
	Возврат СтрЗаменить(ИсходнаяСтрока, Символы.ВК, "");
КонецФункции

