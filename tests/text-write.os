Перем юТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	юТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;
	
	ВсеТесты.Добавить("ТестДолжен_ПроверитьЧтоВозможнаЗаписьUTF8БезBOM");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьЧтоВозможнаЗаписьUTF8БезBOMСПеречислениемКодировкаТекста");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьЧтоНеоткрытыйФайлБросаетПравильноеИсключениеВМетодеЗаписать");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьЧтоНеоткрытыйФайлБросаетПравильноеИсключениеВМетодеЗаписатьСтроку");
	
	Возврат ВсеТесты;
	
КонецФункции

Процедура ТестДолжен_ПроверитьЧтоВозможнаЗаписьUTF8БезBOM() Экспорт
	
	Путь = ПолучитьИмяВременногоФайла("txt");
	ЗТ = Новый ЗаписьТекста(Путь, "windows-1251"); 
	ЗТ.Закрыть(); 
	ЗТ = Новый ЗаписьТекста(Путь,"UTF-8",,Истина);
	ЗТ.Записать("Привет");
	ЗТ.Закрыть();
	
	Файл = Новый Файл(Путь);
	
	Попытка
		юТест.ПроверитьРавенство(12, Файл.Размер());
	Исключение
		УдалитьФайлы(Путь);
		ВызватьИсключение;
	КонецПопытки;
	
	УдалитьФайлы(Путь);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьЧтоВозможнаЗаписьUTF8БезBOMСПеречислениемКодировкаТекста() Экспорт
	
	Путь = ПолучитьИмяВременногоФайла("txt");
	ЗТ = Новый ЗаписьТекста(Путь, КодировкаТекста.ANSI); 
	ЗТ.Закрыть(); 
	ЗТ = Новый ЗаписьТекста(Путь,КодировкаТекста.UTF8,,Истина);
	ЗТ.Записать("Привет");
	ЗТ.Закрыть();
	
	Файл = Новый Файл(Путь);
	
	Попытка
		юТест.ПроверитьРавенство(12, Файл.Размер());
	Исключение
		УдалитьФайлы(Путь);
		ВызватьИсключение;
	КонецПопытки;
	
	УдалитьФайлы(Путь);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьЧтоНеоткрытыйФайлБросаетПравильноеИсключениеВМетодеЗаписать() Экспорт
	
	Запись = Новый ЗаписьТекста();
	
	Попытка
		Запись.Записать("---");
	Исключение
		юТест.ПроверитьРавенство(ИнформацияОбОшибке().Описание, "Файл не открыт", "Метод Записать должен бросать правильное исключение");
		Возврат;
	КонецПопытки;
	
	ВызватьИсключение "Не было брошено исключение";
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьЧтоНеоткрытыйФайлБросаетПравильноеИсключениеВМетодеЗаписатьСтроку() Экспорт
	
	Запись = Новый ЗаписьТекста();
	
	Попытка
		Запись.ЗаписатьСтроку("---");
	Исключение
		юТест.ПроверитьРавенство(ИнформацияОбОшибке().Описание, "Файл не открыт", "Метод ЗаписатьСтроку должен бросать правильное исключение");
		Возврат;
	КонецПопытки;
	
	ВызватьИсключение "Не было брошено исключение";
	
КонецПроцедуры
